scn aaDusfergonSwap

;Main script
;-----------------------------------------------------------------------------
;aaDusfergonManager
;-----------------------------------------------------------------------------

;Used scripts
;-----------------------------------------------------------------------------
;aaDusfergonStatSet2
;-----------------------------------------------------------------------------

;Related scripts
;-----------------------------------------------------------------------------
;aaDusfergonDefaultINI
;aaDusfergonExchange
;-----------------------------------------------------------------------------

;Used globals
;-----------------------------------------------------------------------------
;aaDusfergonEquip
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;Takes two weapons and an enchantment; if the two weapons are different and 
;the one to be replaced is in the Player's inventory, exchange it for the new 
;weapon, copy item values from old to new and assign the passed enchantment if
;the new weapon isn't the Staff. Returns what new self RefID should be.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
float Health
float Charge

ref Item
ref OldWeap
ref NewWeap
ref Ench
;-----------------------------------------------------------------------------



;-----------------------------------------------------------------------------
begin Function { OldWeap, NewWeap, Ench }
;-----------------------------------------------------------------------------
	SetFunctionValue OldWeap
	
	;Swap Old for New if necessary
	;-----------------------------------------------------------------------------
	if ( Player.GetItemCount OldWeap > 0 && OldWeap != NewWeap )
	;-----------------------------------------------------------------------------
		SetFunctionValue NewWeap

		;-----------------------------------------------------------------------------
		;Add NewWeap
		;-----------------------------------------------------------------------------
		Player.AddItemNS NewWeap 1

		;-----------------------------------------------------------------------------
		;Set stats not updated in ModelChange
		;-----------------------------------------------------------------------------
		;Set name and aux stats
		CopyName OldWeap NewWeap
		Call aaDusfergonStatSet2 NewWeap aaDusfergonInit.fWeightBase aaDusfergonInit.fWeightMult aaDusfergonInit.sHealthBase aaDusfergonInit.fHealthMult aaDusfergonInit.lValueBase aaDusfergonInit.fValueMult

		SetObjectCharge aaDusfergonInit.Charge NewWeap

		;The Staff has it's own enchantment that is always set
		if ( aaDusfergonModel < aaDusfergonInit.StaffIdx )
			
			;Set enchantment if necessary
			if ( aaDusfergonlevel >= aaDusfergonInit.sEnchStart )

				SetEnchantment Ench NewWeap	

			else

				RemoveEnchantment NewWeap

			endif

		endif

		;-----------------------------------------------------------------------------
		;Transfer Health and Charge from OldWeap to NewWeap
		;-----------------------------------------------------------------------------
		ForEach ( Item <- PlayerRef )

			if ( Item.GetBaseObject == OldWeap )

				Let Health := Item.GetCurrentHealth
				Let Charge := Item.GetCurrentCharge

			endif

			;Inventory items are indexed in the order added, so NewWeap will always be after OldWeap
			if ( Item.GetBaseObject == NewWeap )

				Item.SetCurrentHealth Health
				Item.SetCurrentCharge Charge

			endif

		loop
		
		;-----------------------------------------------------------------------------
		;Equip if necessary
		;-----------------------------------------------------------------------------
		if ( aaDusfergonEquip == 1 )

			Player.UnEquipItemSilent OldWeap
			Player.EquipItem2NS NewWeap

		endif

		;-----------------------------------------------------------------------------
		;Remove OldWeap
		;-----------------------------------------------------------------------------
		Player.RemoveItemNS OldWeap 1
				
	;-----------------------------------------------------------------------------
	endif ;( Player.GetItemCount OldWeap > 0 )
	;-----------------------------------------------------------------------------

	Return
;-----------------------------------------------------------------------------
end ;Function { OldWeap, NewWeap, Ench }
;-----------------------------------------------------------------------------