scn aaDusfergonEnchSlct

;Related scripts
;-----------------------------------------------------------------------------
;aaDusfergonManager
;aaDusfergonEnchInit
;aaDusfergonEnchLvl
;aaDusfergonIncrVal
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;aaDusfergonEnch
;aaDusfergonEnch1
;aaDusfergonEnch2
;aaDusfergonEnch3
;aaDusfergonEnch4
;aaDusfergonEnch5
;aaDusfergonEnch6
;aaDusfergonEnch7
;aaDusfergonEnch8
;aaDusfergonEnch9
;aaDusfergonEnch10
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;According to globals above, checks if the appropriate effect set has been 
;added to the passed enchantment.  If not, adds the effects with base values 
;defined in Dusfergon.ini.
;Returns total Cost for book keeping.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
long MagnitudeDG
long AreaDG
long DurationDG
long MagnitudeWK
long AreaWK
long DurationWK
long Area
long AreaS
short Range
float Cost ;Return value

short EnchIter
short DGFALv
short WKMALv

ref Ench
;-----------------------------------------------------------------------------



;-----------------------------------------------------------------------------
begin Function { Ench, Range, Cost }
;-----------------------------------------------------------------------------

	;-----------------------------------------------------------------------------
	;Set up Staff's innate Area
	;-----------------------------------------------------------------------------
	if ( Range == 2 )

		Set AreaS to aaDusfergonInit.sStaffAreaBase + ( aaDusfergonInit.fStaffAreaMult * ( aaDusfergonLevel - 1 ) )

	else

		Set AreaS to 0

	endif
	
	;-----------------------------------------------------------------------------
	;Ensure Damage Fatigue and Weakness to Magic are always last
	;-----------------------------------------------------------------------------
	if ( MagicItemHasEffect DGFA Ench )

		if ( MagicItemHasEffect WKMA Ench )

			Set EnchIter to ( GetMagicItemEffectCount Ench ) - 2

		else

			Set EnchIter to ( GetMagicItemEffectCount Ench ) - 1

		endif

		Set MagnitudeDG to GetNthEffectItemMagnitude Ench EnchIter
		Set AreaDG to GetNthEffectItemArea Ench EnchIter
		Set DurationDG to GetNthEffectItemDuration Ench EnchIter

		RemoveNthEffectItem Ench EnchIter

		Set DGFALv to 2

	else

		Set DGFALv to 0

	endif
	
	if ( MagicItemHasEffect WKMA Ench )

		Set EnchIter to ( GetMagicitemEffectCount Ench ) - 1
					
		Set MagnitudeWK to GetNthEffectItemMagnitude Ench EnchIter
		Set AreaWK to GetNthEffectItemArea Ench EnchIter
		Set DurationWK to GetNthEffectItemDuration Ench EnchIter

		RemoveNthEffectItem Ench EnchIter

		Set WKMALv to 2

	else

		Set WKMALv to 0

	endif

	;-----------------------------------------------------------------------------
	;Fire option
	;-----------------------------------------------------------------------------
	if ( aaDusfergonEnch == 1 && aaDusfergonEnch1 == 0 )
	;-----------------------------------------------------------------------------
		if ( WKMALv == 0 )

			Set WKMALv to 1

		endif

		Set Area to AreaS + aaDusfergonInit.sELDGAreaBase
		AddFullEffectItem FIDG aaDusfergonInit.sELDGMagnBase Area aaDusfergonInit.sELDGDuraBase Range Ench
		
		Set Area to AreaS + aaDusfergonInit.sWKELAreaBase
		AddFullEffectItem WKFI aaDusfergonInit.sWKELMagnBase Area aaDusfergonInit.sWKELDuraBase Range Ench

		Set Cost to Cost + ( aaDusfergonInit.fELDGCostBase + aaDusfergonInit.fWKELCostBase )

	;-----------------------------------------------------------------------------
	;Ice option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 2 && aaDusfergonEnch2 == 0 )
	;-----------------------------------------------------------------------------
		if ( WKMALv == 0 )

			Set WKMALv to 1

		endif

		Set Area to AreaS + aaDusfergonInit.sELDGAreaBase
		AddFullEffectItem FRDG aaDusfergonInit.sELDGMagnBase Area aaDusfergonInit.sELDGDuraBase Range Ench
		
		Set Area to AreaS + aaDusfergonInit.sWKELAreaBase
		AddFullEffectItem WKFR aaDusfergonInit.sWKELMagnBase Area aaDusfergonInit.sWKELDuraBase Range Ench

		Set Cost to Cost + ( aaDusfergonInit.fELDGCostBase + aaDusfergonInit.fWKELCostBase )
			
	;-----------------------------------------------------------------------------
	;Volt option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 3 && aaDusfergonEnch3 == 0 )
	;-----------------------------------------------------------------------------
		if ( WKMALv == 0 )

			Set WKMALv to 1

		endif

		Set Area to AreaS + aaDusfergonInit.sELDGAreaBase
		AddFullEffectItem SHDG aaDusfergonInit.sELDGMagnBase Area aaDusfergonInit.sELDGDuraBase Range Ench
		
		Set Area to AreaS + aaDusfergonInit.sWKELAreaBase
		AddFullEffectItem WKSH aaDusfergonInit.sWKELMagnBase Area aaDusfergonInit.sWKELDuraBase Range Ench

		Set Cost to Cost + ( aaDusfergonInit.fELDGCostBase + aaDusfergonInit.fWKELCostBase )

	;-----------------------------------------------------------------------------
	;Knight-Slayer option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 4 && aaDusfergonEnch4 == 0 )
	;-----------------------------------------------------------------------------
		if ( DGFALv == 0 )

			Set DGFALv to 1

		endif

		Set Area to AreaS + aaDusfergonInit.sDIWEAreaBase
		AddFullEffectItem DIWE aaDusfergonInit.sDIWEMagnBase Area aaDusfergonInit.sDIWEDuraBase Range Ench

		Set Area to AreaS + aaDusfergonInit.sDIARAreaBase
		AddFullEffectItem DIAR aaDusfergonInit.sDIARMagnBase Area aaDusfergonInit.sDIARDuraBase Range Ench

		Set Cost to Cost + ( aaDusfergonInit.fDIWECostBase + aaDusfergonInit.fDIARCostBase )

	;-----------------------------------------------------------------------------
	;Mage-Slayer option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 5 && aaDusfergonEnch5 == 0 )
	;-----------------------------------------------------------------------------
		Set Area to AreaS + aaDusfergonInit.sDGSPAreaBase
		AddFullEffectItem DGSP aaDusfergonInit.sDGSPMagnBase Area aaDusfergonInit.sDGSPDuraBase Range Ench

		Set Area to AreaS + aaDusfergonInit.sDSPLAreaBase
		AddFullEffectItem DSPL aaDusfergonInit.sDSPLMagnBase Area 0 Range Ench

		Set Area to AreaS + aaDusfergonInit.sSLNCAreaBase
		AddFullEffectItem SLNC 0 Area aaDusfergonInit.sSLNCDuraBase Range Ench
		
		Set Cost to Cost + ( aaDusfergonInit.fDGSPCostBase + aaDusfergonInit.fDSPLCostBase + aaDusfergonInit.fSLNCCostBase )
			
	;-----------------------------------------------------------------------------
	;Weaken option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 6 && aaDusfergonEnch6 == 0 )
	;-----------------------------------------------------------------------------		
		if ( WKMALv == 0 )

			Set WKMALv to 1

		endif

		Set Area to AreaS + aaDusfergonInit.sBRDNAreaBase
		AddFullEffectItem BRDN aaDusfergonInit.sBRDNMagnBase Area aaDusfergonInit.sBRDNDuraBase Range Ench
		
		Set Area to AreaS + aaDusfergonInit.sDRATAreaBase
		Set EnchIter to AddFullEffectItem DRAT aaDusfergonInit.sDRATMagnBase Area aaDusfergonInit.sDRATDuraBase Range Ench
		SetNthEffectItemActorValue "Strength" Ench EnchIter
		
		Set Cost to Cost + ( aaDusfergonInit.fBRDNCostBase + aaDusfergonInit.fDRATCostBase )

	;-----------------------------------------------------------------------------
	;Subjugate option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 7 && aaDusfergonEnch7 == 0 )
	;-----------------------------------------------------------------------------
		Set Area to AreaS + aaDusfergonInit.sCOMMAreaBase
		AddFullEffectItem COCR aaDusfergonInit.sCOMMMagnBase Area aaDusfergonInit.sCOMMDuraBase Range Ench
		
		Set Area to AreaS + aaDusfergonInit.sCOMMAreaBase
		AddFullEffectItem COHU aaDusfergonInit.sCOMMMagnBase Area aaDusfergonInit.sCOMMDuraBase Range Ench
		
		Set Area to AreaS + aaDusfergonInit.sRALYAreaBase
		AddFullEffectItem RALY aaDusfergonInit.sRALYMagnBase Area aaDusfergonInit.sRALYDuraBase Range Ench

		Set Cost to Cost + ( aaDusfergonInit.fCOMMCostBase + aaDusfergonInit.fCOMMCostBase + aaDusfergonInit.fRALYCostBase )

	;-----------------------------------------------------------------------------
	;Corruption option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 8 && aaDusfergonEnch8 == 0 )
	;-----------------------------------------------------------------------------
		Set Area to AreaS + aaDusfergonInit.sFRNZAreaBase
		AddFullEffectItem FRNZ aaDusfergonInit.sFRNZMagnBase Area aaDusfergonInit.sFRNZDuraBase Range Ench
		
		Set Area to AreaS + aaDusfergonInit.sLGHTAreaBase
		AddFullEffectItem LGHT aaDusfergonInit.sLGHTMagnBase Area aaDusfergonInit.sLGHTDuraBase Range Ench
		
		Set Area to AreaS + aaDusfergonInit.sSTRPAreaBase
		AddFullEffectItem STRP 0 Area aaDusfergonInit.sSTRPDuraBase Range Ench

		Set Cost to Cost + ( aaDusfergonInit.fFRNZCostBase + aaDusfergonInit.fLGHTCostBase + aaDusfergonInit.fSTRPCostBase )

	;-----------------------------------------------------------------------------
	;Unbalance option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 9 && aaDusfergonEnch9 == 0 )
	;-----------------------------------------------------------------------------		
		if ( DGFALv == 0 )

			Set DGFALv to 1

		endif

		if ( WKMALv == 0 )

			Set WKMALv to 1

		endif

		
		Set Area to AreaS + aaDusfergonInit.sDRATAreaBase
		Set EnchIter to AddFullEffectItem DRAT aaDusfergonInit.sDRATMagnBase Area aaDusfergonInit.sDRATDuraBase Range Ench
		SetNthEffectItemActorValue "Agility" Ench EnchIter

		Set Cost to Cost + ( aaDusfergonInit.fDRATCostBase )

	;-----------------------------------------------------------------------------
	;Siphon option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 10 && aaDusfergonEnch10 == 0 )
	;-----------------------------------------------------------------------------
		Set Area to AreaS + aaDusfergonInit.sABFAAreaBase
		AddFullEffectItem ABFA aaDusfergonInit.sABFAMagnBase Area aaDusfergonInit.sABFADuraBase Range Ench
		
		Set Area to AreaS + aaDusfergonInit.sABHEAreaBase
		AddFullEffectItem ABHE aaDusfergonInit.sABHEMagnBase Area aaDusfergonInit.sABHEDuraBase Range Ench
		
		Set Area to AreaS + aaDusfergonInit.sABSPAreaBase
		AddFullEffectItem ABSP aaDusfergonInit.sABSPMagnBase Area aaDusfergonInit.sABSPDuraBase Range Ench

		Set Cost to Cost + ( aaDusfergonInit.fABFACostBase + aaDusfergonInit.fABHECostBase + aaDusfergonInit.fABSPCostBase )

	;-----------------------------------------------------------------------------
	endif ;( aaDusfergonEnch != 0 )
	;-----------------------------------------------------------------------------

	;-----------------------------------------------------------------------------
	;Append Damage Fatigue and Weakness to Magic
	;-----------------------------------------------------------------------------
	if ( DGFALv > 0 )

		if ( DGFALv == 1 )

			Set MagnitudeDG to aaDusfergonInit.sDGFAMagnBase
			Set AreaDG to AreaS + aaDusfergonInit.sDGFAAreaBase
			Set DurationDG to aaDusfergonInit.sDGFADuraBase				

			Set Cost to Cost + aaDusfergonInit.fDGFACostBase

		endif

		AddFullEffectItem DGFA MagnitudeDG AreaDG DurationDG Range Ench

	endif
	
	if ( WKMALv > 0 )

		if ( WKMALv == 1 )

			Set MagnitudeWK to aaDusfergonInit.sWKMAMagnBase
			Set AreaWK to AreaS + aaDusfergonInit.sWKMAAreaBase
			Set DurationWK to aaDusfergonInit.sWKMADuraBase			

			Set Cost to Cost + aaDusfergonInit.fWKMACostBase

		endif

		AddFullEffectItem WKMA MagnitudeWK AreaWK DurationWK Range Ench

	endif


	SetEnchantmentCost Cost Ench

	SetFunctionValue Cost
	Return
;-----------------------------------------------------------------------------
end ;Function { Ench, Range, Cost }
;-----------------------------------------------------------------------------