scn aaDusfergonStatLvlS

;Related scripts
;-----------------------------------------------------------------------------
;aaDusfergonManager
;aaDusfergonStatLvl
;aaDusfergonEnchLvl
;aaDusfergonIncrVal
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;aaDusfergonLevel
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;Increments the values of DGHE and Script effect by the values defined in the
;.ini, then increments all effects' Area by the amount defined in 
;fStaffAreaMult for the passed Ench.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
long Magnitude
long Area
long Duration
float Cost ;Return value

float MagM
float AreM
float DurM
float AreaSM
float CostM

short EnchIter
short EnchIndex

ref Ench
;-----------------------------------------------------------------------------



;-----------------------------------------------------------------------------
begin Function { Ench, MagM, AreM, DurM, CostM, AreaSM, Cost }
;-----------------------------------------------------------------------------
	Set EnchIndex to GetMagicItemEffectCount Ench

	;-----------------------------------------------------------------------------
	;Increment Damage Health and Script effect
	;-----------------------------------------------------------------------------
	Set Magnitude to Call aaDusfergonIncrVal MagM ( aaDusfergonLevel - 1 )
	Set Area to Call aaDusfergonIncrVal AreM ( aaDusfergonLevel - 1 )
	Set Duration to Call aaDusfergonIncrVal DurM ( aaDusfergonLevel - 1 )

	Set Cost to Cost + CostM

	Set EnchIter to 0
	ModNthEffectItemArea Area Ench EnchIter

	Set EnchIter to 1
	ModNthEffectItemMagnitude Magnitude Ench EnchIter
	ModNthEffectItemArea Area Ench EnchIter
	ModNthEffectItemDuration Duration Ench EnchIter

	SetEnchantmentCost Cost Ench

	;-----------------------------------------------------------------------------
	;Increment Areas of all effects
	;-----------------------------------------------------------------------------
	Set EnchIter to 0
	While ( EnchIter < EnchIndex )

		Set Area to Call aaDusfergonIncrVal AreaSM ( aaDusfergonLevel - 1 )

		ModNthEffectItemArea Area Ench EnchIter

		Set EnchIter to ( EnchIter + 1 )	
	loop


	SetFunctionValue Cost
	Return
;-----------------------------------------------------------------------------
end ;Function { Ench, Magnitude, Area, Duration, CostM, AreaSM, Cost }
;-----------------------------------------------------------------------------