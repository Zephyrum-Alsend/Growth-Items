scn aaDusfergonSwap

;Related scripts
;-----------------------------------------------------------------------------
;aaDusfergonManager
;aaDusfergonOnHit
;aaDusfergonOnBowHit
;aaDusfergonOnArrowHit
;aaDusfergonStatSet2
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;aaDusfergonEquip
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;Used by ModelChange block in Manager to swap to and from Bow form.
;Takes two weapons and an enchantment; if the weapon to be replaced is in the
;Player's inventory, exchange it for the new weapon and copy item values from
;old to new.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
float Health
float Charge

ref CurWeap
ref OldWeap
ref NewWeap
ref Ench
;-----------------------------------------------------------------------------



;-----------------------------------------------------------------------------
begin Function { OldWeap, NewWeap, Ench }
;-----------------------------------------------------------------------------
	;Swap Old for New if necessary
	;-----------------------------------------------------------------------------
	if ( Player.GetItemCount OldWeap > 0 )
	;-----------------------------------------------------------------------------

		;-----------------------------------------------------------------------------
		;Get the old Charge and Health
		;-----------------------------------------------------------------------------
		if ( aaDusfergonEquip == 0 )

			Set CurWeap to Player.GetEquippedObject 16
			
			Player.EquipItemSilent OldWeap
			Set Health to Player.GetEquippedCurrentHealth 16
			Set Charge to Player.GetEquippedCurrentCharge 16
			Player.UnEquipItemSilent OldWeap

			Player.EquipItemSilent CurWeap

		else

			Set Health to Player.GetEquippedCurrentHealth 16
			Set Charge to Player.GetEquippedCurrentCharge 16
			
		endif

		;-----------------------------------------------------------------------------
		;Set stats not updated in ModelChange
		;-----------------------------------------------------------------------------
		;Set new self
		Set aaDusfergonInit.self to NewWeap
		
		;Set name and aux stats
		CopyName OldWeap NewWeap
		Call aaDusfergonStatSet2 NewWeap aaDusfergonInit.fWeightBase aaDusfergonInit.fWeightMult aaDusfergonInit.sHealthBase aaDusfergonInit.fHealthMult aaDusfergonInit.lValueBase aaDusfergonInit.fValueMult

		;Set enchantment if necessary
		if ( aaDusfergonlevel >= aaDusfergonInit.sEnchStart )

			SetEnchantment Ench NewWeap
			SetObjectCharge aaDusfergonInit.Charge NewWeap

		else

			RemoveEnchantment NewWeap

		endif

		;-----------------------------------------------------------------------------
		;Swap old for new
		;-----------------------------------------------------------------------------
		Player.AddItemNS NewWeap 1

		;Equip if necessary
		if ( aaDusfergonEquip == 1 )

			RemoveEventHandler "OnHitWith" aaDusfergonOnhit
			RemoveEventHandler "OnRelease" aaDusfergonOnBowHit
			;Leave OnArrowHit up so still airborne arrows grant XP
			Player.EquipItem2NS NewWeap

		endif

		Player.RemoveItemNS OldWeap 1

		;-----------------------------------------------------------------------------
		;Set the new Charge and Health
		;-----------------------------------------------------------------------------
		if ( aaDusfergonEquip == 0 )

			Player.EquipItemSilent NewWeap
			Player.SetEquippedCurrentHealth Health 16
			Player.SetEquippedCurrentCharge Charge 16
			Player.UnEquipItemSilent NewWeap

			Player.EquipItemSilent CurWeap

		else

			Player.SetEquippedCurrentHealth Health 16
			Player.SetEquippedCurrentCharge Charge 16
			
		endif
				
	;-----------------------------------------------------------------------------
	endif ;( Player.GetItemCount OldWeap > 0 )
	;-----------------------------------------------------------------------------

	Return
;-----------------------------------------------------------------------------
end ;Function { OldWeap, NewWeap, Ench }
;-----------------------------------------------------------------------------