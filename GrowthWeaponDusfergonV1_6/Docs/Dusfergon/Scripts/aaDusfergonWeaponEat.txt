scn aaDusfergonWeaponEat

;Main script
;-----------------------------------------------------------------------------
;aaDusfergonManger
;-----------------------------------------------------------------------------

;Used scripts
;-----------------------------------------------------------------------------
;aaDusfergonRefreshInv
;-----------------------------------------------------------------------------

;Related scripts
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Used globals
;-----------------------------------------------------------------------------
;aaDusfergonLevel
;aaDusfergonXP
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
short LvlCap
short Eater ;Return value
float Absor
short XPGain

string_var Prompt
string_var Name
string_var Icon

ref Weap
ref Item
;-----------------------------------------------------------------------------



;-----------------------------------------------------------------------------
begin Function { LvlCap, Eater, Weap, Name, Icon, Absor }
;-----------------------------------------------------------------------------

	;-----------------------------------------------------------------------------
	;Get the Player's response
	;-----------------------------------------------------------------------------
	if ( aaDusfergonInit.Button < 0 )

		Set aaDusfergonInit.Button to GetButtonPressed

	endif



	;-----------------------------------------------------------------------------
	;If at or exceeding sLevelCap, refuse to eat
	;-----------------------------------------------------------------------------
	if ( aaDusfergonLevel >= LvlCap )
	;-----------------------------------------------------------------------------
	
		Let Prompt := Name + " has no need for further offerings."		
		MessageBox $Prompt

		Set Eater to 0
		
	;-----------------------------------------------------------------------------
	;Confirm with Player if they really want to feed Weap to weapon
	;-----------------------------------------------------------------------------
	elseif ( Eater == 1 )
	;-----------------------------------------------------------------------------
	
		Set Prompt to sv_Construct "Do you want to offer %n to %z?" Weap Name
		MessageBox $Prompt "Yes" "No"

		Set Eater to 2

	;-----------------------------------------------------------------------------
	;Feed Weap to weapon
	;-----------------------------------------------------------------------------
	elseif ( Eater == 2 && aaDusfergonInit.Button > -1 )
	;-----------------------------------------------------------------------------
	
		;-----------------------------------------------------------------------------
		;Button == Yes
		;-----------------------------------------------------------------------------
		if ( aaDusfergonInit.Button == 0 )

			;-----------------------------------------------------------------------------
			;State item's consumption and play item break SFX
			;-----------------------------------------------------------------------------
			SetMessageIcon $Icon
			SetMessageSound UIArmorWeaponRepairBreak
			MessageEX "%z consumes %n." Name Weap

			;-----------------------------------------------------------------------------
			printd "Iterating through player inventory..."
			;-----------------------------------------------------------------------------
			foreach ( Item <- PlayerRef ) 

				printd "Comparing Item to Weap"
				if ( Item.GetBaseObject == Weap )

					printd "Found Weap, calculating XPGain"
					Let XPGain := Absor * Item.GetCurrentHealth

					printd "Removing Item"
					Item.RemoveMeIR

					Break
			
				endif
		
			loop

			
			printd "Refreshing player inventory..."
			Call aaDusfergonRefreshInv

			printd "Adding XP"
			Let aaDusfergonXP := aaDusfergonXP + XPGain

			;-----------------------------------------------------------------------------
			printc "%z gained %.0fXP from consuming %n." Name XPGain Weap
			;-----------------------------------------------------------------------------

		;-----------------------------------------------------------------------------
		endif ;( Button == Yes )
		;-----------------------------------------------------------------------------
	
		Set Eater to 0
		
	;-----------------------------------------------------------------------------
	endif ;( Level < sLevelCap && Eater > 0 )
	;-----------------------------------------------------------------------------
	
	
	SetFunctionValue Eater
	Return
;-----------------------------------------------------------------------------
end ;Function { LvlCap, Eater, Weap, Name, Icon, Absor }
;-----------------------------------------------------------------------------