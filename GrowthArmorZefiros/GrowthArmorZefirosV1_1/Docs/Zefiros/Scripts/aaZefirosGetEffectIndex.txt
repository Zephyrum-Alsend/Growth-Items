scn aaZefirosGetEffectIndex

;Main script
;-----------------------------------------------------------------------------
;aaZefirosManager
;-----------------------------------------------------------------------------

;Used scripts
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Related scripts
;-----------------------------------------------------------------------------
;aaZefirosSetEffectItem
;aaZefirosIncrementEnchant
;aaZefirosSetMergeEnch
;aaZefirosSetSeek
;-----------------------------------------------------------------------------

;Used globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;Given an effectCode and ActorValueCode, returns the index of the effect item
;on Ench.  Returns -1 if effect not found.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
long EffC
long AuxC
ref Ench

short Count
short Idx ;Return value
;-----------------------------------------------------------------------------



;-----------------------------------------------------------------------------
begin Function { EffC, AuxC, Ench }
;-----------------------------------------------------------------------------

	;MagicItemHasEffectCode does not properly handle invalid ActorValue codes,
	;so we have two separate calls based on the code passed
	
	;-----------------------------------------------------------------------------
	;Effect does not affect an Attribute or Skill
	;-----------------------------------------------------------------------------
	if ( AuxC == 256 && MagicItemHasEffectCode EffC Ench )
	;-----------------------------------------------------------------------------

		Let Count := GetMagicItemEffectCount Ench
		Let Idx := 0
		While ( Idx < Count )

			if ( EffC == GetNthEffectItemCode Ench Idx )

				SetFunctionValue Idx
				Break

			endif

			
			Let Idx += 1
		loop
		Return

	;-----------------------------------------------------------------------------
	;Effect does affect an Attribute or Skill
	;-----------------------------------------------------------------------------
	elseif ( MagicItemHasEffectCode EffC Ench AuxC )
	;-----------------------------------------------------------------------------

		Let Count := GetMagicItemEffectCount Ench
		Let Idx := 0
		While ( Idx < Count )

			if ( EffC == GetNthEffectItemCode Ench Idx && AuxC == GetNthEffectItemActorValue Ench Idx )

				SetFunctionValue Idx
				Break
				
			endif


			Let Idx += 1
		loop
		Return
		
	;-----------------------------------------------------------------------------
	;Effect does not exist
	;-----------------------------------------------------------------------------
	else
	;-----------------------------------------------------------------------------

		SetFunctionValue -1
		Return

	;-----------------------------------------------------------------------------
	endif ;( MagicItemHasEffectCode EffC Ench (AuxC) )
	;-----------------------------------------------------------------------------
	

	;Failsafe
	SetFunctionValue -1
	Return
;-----------------------------------------------------------------------------
end ;Function { EffC, AuxC, Ench }
;-----------------------------------------------------------------------------