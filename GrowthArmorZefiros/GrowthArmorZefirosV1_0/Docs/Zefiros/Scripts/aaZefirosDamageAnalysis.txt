scn aaZefirosDamageAnalysis

;The quest this is attached to, 
;and therefore this script, 
;run every this second(s).
float fQuestDelayTime

;Main script
;-----------------------------------------------------------------------------
;aaZefirosManager
;-----------------------------------------------------------------------------

;Used scripts
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Related scripts
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Used globals
;-----------------------------------------------------------------------------
;aaZefirosXP
;aaZefirosRLogFire
;aaZefirosRLogFrost
;aaZefirosRLogShock
;aaZefirosRLogMagic
;aaZefirosRLogNormal
;aaZefirosRLogPhysical
;aaZefirosRLogParalysis
;aaZefirosRXPFire
;aaZefirosRXPFrost
;aaZefirosRXPShock
;aaZefirosRXPMagic
;aaZefirosRXPNormal
;aaZefirosRXPParalysis
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;With flags set by OnMagicHit and OnWeaponHit, attribute damage taken to
;various damage types and distribute XP to related resistances.  Re-evaluate
;flag conditions by testing if Player is still affected by said damage types.
;
;XP is evenly distributed between different damage types instead of accurately
;awarded based on damage per type.  This is a design decision due to limited
;tools.  It is impossible to directly pull how much damage was taken on a per
;type basis.  It is theoretically possible to get exactly how much damage of 
;each type was taken, but would require usage of presently unreliable handlers
;and massively increase the computation time as we pull all the data needed
;to reconstruct the damage formula.  It is likely to take more than 1 frame,
;which is all the time we have.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
float XP
float LastXP
short Div
short ParaXP
;-----------------------------------------------------------------------------

;-----------------------------------------------------------------------------
begin GameMode
;-----------------------------------------------------------------------------

	;-----------------------------------------------------------------------------
	;Initialize variables
	;-----------------------------------------------------------------------------
	if ( GetGameLoaded )

		;fQuestDelayTime is set within Manager
		;ParaXP is set within Manager
		Let XP := 0
		Let LastXP := aaZefirosXP

	endif

	;-----------------------------------------------------------------------------
	;Get XP accrued between invocations of this script
	;-----------------------------------------------------------------------------
	Let XP := aaZefirosXP - LastXP
	Let LastXP := aaZefirosXP

	;-----------------------------------------------------------------------------
	;Determine the divisor
	;-----------------------------------------------------------------------------
	Let Div := 0

	;Fire
	if ( aaZefirosRLogFire == 1 )

		Let Div += 1

	endif

	;Frost
	if ( aaZefirosRLogFrost == 1 )

		Let Div += 1

	endif

	;Shock
	if ( aaZefirosRLogShock == 1 )

		Let Div += 1

	endif

	;Non-Elemental
	if ( aaZefirosRLogMagic == 1 )

		Let Div += 1

	endif

	;Normal Weapon
	if ( aaZefirosRLogNormal == 1 )

		Let Div += 1

	endif

	;Non-Normal Weapon
	if ( aaZefirosRLogPhysical == 1 )

		Let Div += 1

	endif

	;-----------------------------------------------------------------------------
	;Attribute XP and re-evaluate log flags
	;-----------------------------------------------------------------------------
	;If Div > 0 and we have more than just Non-Normal Weapon damage
	;-----------------------------------------------------------------------------
	if eval( Div > 0 && !( Div == 1 && aaZefirosRLogPhysical == 1 ) )
	;-----------------------------------------------------------------------------

		Let XP := XP / Div

		DebugPrint "Detected more than Non-Normal Weapon damage, adding %.2f Resistance XP." XP


		;-----------------------------------------------------------------------------
		;Fire
		;-----------------------------------------------------------------------------
		if ( aaZefirosRLogFire == 1 )

			Let aaZefirosRXPFire += XP

			if ( Player.HasMagicEffect FIDG )

				DebugPrint "Still taking Fire damage..."

			else

				DebugPrint "No longer taking Fire damage."
				Let aaZefirosRLogFire := 0

			endif

		endif


		;-----------------------------------------------------------------------------
		;Frost
		;-----------------------------------------------------------------------------
		if ( aaZefirosRLogFrost == 1 )

			Let aaZefirosRXPFrost += XP

			if ( Player.HasMagicEffect FRDG )
	
				DebugPrint "Still taking Frost damage..."
	
			else

				DebugPrint "No longer taking Frost damage."
				Let aaZefirosRLogFrost := 0

			endif

		endif


		;-----------------------------------------------------------------------------
		;Shock
		;-----------------------------------------------------------------------------
		if ( aaZefirosRLogShock == 1 )

			Let aaZefirosRXPShock += XP

			if ( Player.HasMagicEffect SHDG )

				DebugPrint "Still taking Shock damage..."

			else

				DebugPrint "No longer taking Shock damage."
				Let aaZefirosRLogShock := 0

			endif

		endif


		;-----------------------------------------------------------------------------
		;Non-Elemental
		;-----------------------------------------------------------------------------
		if ( aaZefirosRLogMagic == 1 )

			Let aaZefirosRXPMagic += XP

			if ( Player.HasMagicEffect DGHE )

				DebugPrint "Still taking Health damage..."

			else

				DebugPrint "No longer taking Health damage."
				Let aaZefirosRLogMagic := 0

			endif

		endif


		;-----------------------------------------------------------------------------
		;Normal Weapon
		;-----------------------------------------------------------------------------
		if ( aaZefirosRLogNormal == 1 )

			Let aaZefirosRXPNormal += XP
			Let aaZefirosRLogNormal := 0

		endif


	;-----------------------------------------------------------------------------
	endif ;( Div > 0 && !( Div == 1 && aaZefirosRLogPhysical == 1 ) )
	;-----------------------------------------------------------------------------
	Let aaZefirosRLogPhysical := 0

	;-----------------------------------------------------------------------------
	;Paralysis
	;-----------------------------------------------------------------------------
	if ( aaZefirosRLogParalysis == 1 )

		;Paralysis is time based instead of damage based
		Let aaZefirosRXPParalysis += ParaXP

		if ( Player.HasMagicEffect PARA )

			DebugPrint "Still Paralyzed..."

		else

			DebugPrint "No longer Paralyzed."
			Let aaZefirosRLogParalysis := 0

		endif

	endif

;-----------------------------------------------------------------------------
end ;GameMode
;-----------------------------------------------------------------------------