scn aaZefirosObjectLegs

;Main script
;-----------------------------------------------------------------------------
;aaZefirosManager
;-----------------------------------------------------------------------------

;Used scripts
;-----------------------------------------------------------------------------
;aaZefirosOnDamage
;aaZefirosOnMagicHit
;aaZefirosOnWeaponHit
;aaZefirosOnMagicApply
;aaZefirosCleanUp
;-----------------------------------------------------------------------------

;Related scripts
;-----------------------------------------------------------------------------
;aaZefirosOnUnEquip
;aaZefirosObjectBody
;aaZefirosObjectFoot
;aaZefirosObjectHand
;aaZefirosObjectHead
;aaZefirosObjectShld
;-----------------------------------------------------------------------------

;Used globals
;-----------------------------------------------------------------------------
;aaZefirosEquip
;aaZefirosSlotLegs
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;Increments Equip flag when equipped by the Player, sets handlers if needed.
;Runs CleanUp when dropped by the Player.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;-----------------------------------------------------------------------------
begin OnEquip Player
;-----------------------------------------------------------------------------

	DebugPrint "Running Legs OnEquip block."

	if ( aaZefirosEquip <= 0 )

		DebugPrint "aaZefirosEquip is %.0f, setting to 1 and setting handlers." aaZefirosEquip

		Let aaZefirosEquip := 0
		SetEventHandler "OnHealthDamage" aaZefirosOnDamage "object"::Player
		SetEventHandler "OnMagicEffectHit" aaZefirosOnMagicHit first::Player
		SetEventHandler "OnHitWith" aaZefirosOnWeaponHit first::Player
		SetEventHandler "OnMagicApply" aaZefirosOnMagicApply second::Player

	endif

	if ( aaZefirosEquip < 6 )

		Let aaZefirosEquip += 1

		;Compensate for merged parts
		if eval( aaZefirosSlotLegs & 4 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotLegs & 32 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotLegs & 16 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotLegs & 1 || aaZefirosSlotLegs & 2 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotLegs & 8192 )
			Let aaZefirosEquip += 1
		endif
		
	endif

;-----------------------------------------------------------------------------
end
;-----------------------------------------------------------------------------

;-----------------------------------------------------------------------------
begin OnDrop Player
;-----------------------------------------------------------------------------

	DebugPrint "Running OnDrop block."
	Call aaZefirosCleanUp 0

;-----------------------------------------------------------------------------
end ;OnDrop Player
;-----------------------------------------------------------------------------