scn aaZefirosOnDamage

;Main script
;-----------------------------------------------------------------------------
;aaZefirosManager
;-----------------------------------------------------------------------------

;Used scripts
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Related scripts
;-----------------------------------------------------------------------------
;aaZefirosDamageAnalysis
;aaZefirosSetHandlers
;aaZefirosOnMagicApply
;aaZefirosOnMagicHit
;aaZefirosOnWeaponHit
;-----------------------------------------------------------------------------

;Used globals
;-----------------------------------------------------------------------------
;aaZefirosXPLog
;aaZefirosXPCombat
;aaZefirosXP
;aaZefirosEquip
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;Fires whenever Player's Health is damaged.
;If Attacker is valid, starts Combat Log and awards XP based on Damage and 
;number of pieces worn.  Attacker is invalid if self harm or environmental.
;If sInvalidAttacker is set in INI, also count invalid Attackers.
;
;Damage over time effects tick every frame, so it is recommended to comment
;out DebugPrint when dealing with them.
;
;For some reason AoE Target spells fired by the Player trigger this, so filter
;out Attacker == Player to fix that.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
float Damage
ref Attacker
;-----------------------------------------------------------------------------

;-----------------------------------------------------------------------------
begin Function { Damage, Attacker }
;-----------------------------------------------------------------------------

	Let Damage := -Damage ;Damage is a negative value
	
	;Only add XP if the damage was external
	;or environmental/self w/ InvalidAttacker flag set
	if ( Attacker != PlayerRef && ( aaZefirosInit.sInvalidAttacker || Attacker ) )

		DebugPrint "Took %.4f damage from %n." Damage Attacker

		;Start XP logger
		if ( aaZefirosXPLog == 0 )

			Let aaZefirosXPLog := 1
			Let aaZefirosXPCombat := aaZefirosXP

		endif

		;Add XP
		Let aaZefirosXP += ( Damage * aaZefirosEquip / 6 )

	else

		DebugPrint "Took %.4f invalid damage." Damage

	endif

	
;-----------------------------------------------------------------------------
end ;Function { Damage, Attacker }
;-----------------------------------------------------------------------------