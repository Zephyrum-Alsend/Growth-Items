scn aaZefirosModStat

;Main script
;-----------------------------------------------------------------------------
;aaZefirosManager
;-----------------------------------------------------------------------------

;Used scripts
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Related scripts
;-----------------------------------------------------------------------------
;aaZefirosDefaultINI
;-----------------------------------------------------------------------------

;Used globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;aaZefirosLevel
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;Mods the stats of the passed armor according to the other values passed.
;
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
short SLvl
short ELvl

short ARB
float WgtB
short HltB
long ValB

short ARM
float WgtM
float HltM
float ValM

float HvyAR
float HvyWgt
short HvyFlg

short Armor
float Weight
long Health
long Value

ref Armr

array_var Stat
;-----------------------------------------------------------------------------



;-----------------------------------------------------------------------------
begin Function { Stat, HvyAR, HvyWgt, HvyFlg, SLvl, ELvl, Armr }
;-----------------------------------------------------------------------------

	;At Lv1, stat == Base
	Let SLvl -= 1
	Let ELvl -= 1

	Let ARB := Stat[0]
	Let WgtB := Stat[1]
	Let HltB := Stat[2]
	Let ValB := Stat[3]
	Let ARM := Stat[4]
	Let WgtM := Stat[5]
	Let HltM := Stat[6]
	Let ValM := Stat[7]


	;Store intermediary result to truncate
	Let Armor := ( ARM * SLvl )
	Let Weight := ( WgtM * SLvl )
	Let Health := ( HltM * SLvl )

	Let Armor := ( ARM * ELvl ) - Armor
	Let Weight := ( WgtM * ELvl ) - Weight
	Let Health := ( HltM * ELvl ) - Health

	;Multiply result by Heavy Armor modifier if necessary
	if ( HvyFlg != 0 )

		Let Armor *= HvyAr
		Let Weight *= HvyWgt

	endif

	;Without ModGoldValue_T, we have to recalculate it instead
	Let Value := ValB + ( ValM * ELvl )


	DebugPrint "Modding %n 	AR by %.0f, 	Weight by %.2f, 	Health by %.0f, 	Value to %.0f." Armr Armor Weight Health Value
	ModArmorAR Armor Armr
	ModWeight Weight Armr
	ModObjectHealth Health Armr
	SetGoldValue_T Value Armr
	;_T makes Value not be stored in the save, like all other stats


	Return
;-----------------------------------------------------------------------------
end ;Function { Stat, HvyAR, HvyWgt, HvyFlg, SLvl, ELvl, Armr }
;-----------------------------------------------------------------------------