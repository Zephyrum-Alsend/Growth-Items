scn aaZefirosCleanUp

;Main script
;-----------------------------------------------------------------------------
;aaZefirosManager
;-----------------------------------------------------------------------------

;Used scripts
;-----------------------------------------------------------------------------
;aaZefirosOnUnEquip
;-----------------------------------------------------------------------------

;Related scripts
;-----------------------------------------------------------------------------
;aaZefirosDefaultINI
;aaZefirosObjectBody
;aaZefirosObjectFoot
;aaZefirosObjectHand
;aaZefirosObjectHead
;aaZefirosObjectLegs
;aaZefirosObjectShld
;aaZefirosFlagCleanOnce
;-----------------------------------------------------------------------------

;Used globals
;-----------------------------------------------------------------------------
;aaZefirosEquip
;aaZefirosSlotBody
;aaZefirosSlotFoot
;aaZefirosSlotHand
;aaZefirosSlotHead
;aaZefirosSlotLegs
;aaZefirosSlotShld
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;Purges non-permanent handlers and re-evaluates conditions for setting them.
;If Mode = 1, also purge permanent handlers and Manager arrays and bounds
;ini exposed variables that could cause problems.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
short Mode
;-----------------------------------------------------------------------------

;-----------------------------------------------------------------------------
begin Function { Mode }
;-----------------------------------------------------------------------------

	if ( Mode == 1 )

		;When loading a save, some data is changed, some data is lost, the rest lingers
		;This section exists to clear that lingering data

		;-----------------------------------------------------------------------------
		DebugPrint "Purging OnActorUnequip handlers."
		;-----------------------------------------------------------------------------
		RemoveEventHandler "OnActorUnequip" aaZefirosOnUnEquip

		;-----------------------------------------------------------------------------
		DebugPrint "Nulling Selfs, Enchs, EncSt, ArmSt, Forms, FSets, DfIdx, Equip, Seek, Actv, Dura, Slots, Dflt, Soul, Menu, Stack, Item arrays."
		;-----------------------------------------------------------------------------
		Let aaZefirosInit.Selfs := ar_Null
		Let aaZefirosInit.Enchs := ar_Null
		Let aaZefirosInit.EncSt := ar_Null
		Let aaZefirosInit.ArmSt := ar_Null
		Let aaZefirosInit.Forms := ar_Null
		Let aaZefirosInit.FSets := ar_Null
		Let aaZefirosInit.DfIdx := ar_Null
		Let aaZefirosInit.Equip := ar_Null
		Let aaZefirosInit.Seek := ar_Null
		Let aaZefirosInit.Actv := ar_Null
		Let aaZefirosInit.Dura := ar_Null
		Let aaZefirosInit.Slots := ar_Null
		Let aaZefirosInit.Dflt := ar_Null
		Let aaZefirosInit.Soul := ar_Null
		Let aaZefirosInit.Menu := ar_Null
		Let aaZefirosInit.Stack := ar_Null
		Let aaZefirosInit.Item := ar_Null

		
		;-----------------------------------------------------------------------------
		DebugPrint "Bounding ini exposed variables."
		;-----------------------------------------------------------------------------
		if ( aaZefirosInit.fXPReqBase < 1 )
			Let aaZefirosInit.fXPReqBase := 1
		endif
		if ( aaZefirosInit.fXPReqMult < 0 )
			Let aaZefirosInit.fXPReqMult := 0
		endif
		if ( aaZefirosInit.sLevelCap < 1 )
			Let aaZefirosInit.sLevelCap := 1
		endif
		if ( aaZefirosInit.fEnchantAbs < 0 )
			Let aaZefirosInit.fEnchantAbs := 0
		endif
		if ( aaZefirosInit.fXPPara < 0 )
			Let aaZefirosInit.fXPPara := 0
		endif
		if ( aaZefirosInit.fXPDisease < 0 )
			Let aaZefirosInit.fXPDisease := 0
		endif
		if ( aaZefirosInit.fXPNoDmg < 0 )
			Let aaZefirosInit.fXPNoDmg := 0
		endif
		if ( aaZefirosInit.fHeavyAR <= 0 )
			Let aaZefirosInit.fHeavyAR := 1.5
		endif
		if ( aaZefirosInit.fHeavyWeight <= 0 )
			Let aaZefirosInit.fHeavyWeight := 2
		endif
		if ( aaZefirosInit.sEnchStart < 2 )
			Let aaZefirosInit.sEnchStart := 2
		endif
		if ( aaZefirosInit.sEnchSeek < 1 )
			Let aaZefirosInit.sEnchSeek := 1
		endif
		if ( aaZefirosInit.fDamageLag <= 0 )
			Let aaZefirosInit.fDamageLag := 0.1
		endif

	endif

	;-----------------------------------------------------------------------------
	;Purge handlers
	;-----------------------------------------------------------------------------
	Call aaZefirosSetHandlers 0

	;-----------------------------------------------------------------------------
	DebugPrint "Re-evaluating Equip flag..."
	;-----------------------------------------------------------------------------
	Let aaZefirosEquip := 0

	;Check if each item is equipped
	if ( Player.GetEquipped DLScalar4Body )

		Let aaZefirosEquip += 1

		;Check if merged with other items
		;Compares bit masks returned by GetBipedSlotMask
		if eval( aaZefirosSlotBody & 32 ) ;Feet
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotBody & 16 ) ;Hands
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotBody & 3 ) ;Head/Hair
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotBody & 8 ) ;LowerBody
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotBody & 8192 ) ;Shield
			Let aaZefirosEquip += 1
		endif

	endif
	if ( Player.GetEquipped DLScalar4Foot )

		Let aaZefirosEquip += 1

		if eval( aaZefirosSlotFoot & 4 ) ;UpperBody
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotFoot & 16 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotFoot & 3 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotFoot & 8 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotFoot & 8192 )
			Let aaZefirosEquip += 1
		endif

	endif
	if ( Player.GetEquipped DLScalar4Hand )

		Let aaZefirosEquip += 1

		if eval( aaZefirosSlotHand & 4 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotHand & 32 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotHand & 3 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotHand & 8 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotHand & 8192 )
			Let aaZefirosEquip += 1
		endif

	endif
	if ( Player.GetEquipped DLScalar4Head )

		Let aaZefirosEquip += 1

		if eval( aaZefirosSlotHead & 4 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotHead & 32 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotHead & 16 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotHead & 8 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotHead & 8192 )
			Let aaZefirosEquip += 1
		endif

	endif
	if ( Player.GetEquipped DLScalar4Legs )

		Let aaZefirosEquip += 1

		if eval( aaZefirosSlotLegs & 4 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotLegs & 32 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotLegs & 16 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotLegs & 3 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotLegs & 8192 )
			Let aaZefirosEquip += 1
		endif

	endif
	if ( Player.GetEquipped DLScalar4Shld )

		Let aaZefirosEquip += 1

		if eval( aaZefirosSlotShld & 4 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotShld & 32 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotShld & 16 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotShld & 3 )
			Let aaZefirosEquip += 1
		endif
		if eval( aaZefirosSlotShld & 8 )
			Let aaZefirosEquip += 1
		endif

	endif

	DebugPrint "Equip flag re-evaluated."

	;-----------------------------------------------------------------------------
	;Reset purged handlers if necessary
	;-----------------------------------------------------------------------------
	if ( aaZefirosEquip > 0 )

		Call aaZefirosSetHandlers 1

	endif


	;-----------------------------------------------------------------------------
	DebugPrint "Finished CleanUp."
	;-----------------------------------------------------------------------------
	Return
;-----------------------------------------------------------------------------
end ;Function { Mode }
;-----------------------------------------------------------------------------