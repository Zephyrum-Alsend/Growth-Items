scn aaZefirosSetMergeEnch

;Main script
;-----------------------------------------------------------------------------
;aaZefirosManager
;-----------------------------------------------------------------------------

;Used scripts
;-----------------------------------------------------------------------------
;aaZefirosGetEffectIndex
;-----------------------------------------------------------------------------

;Related scripts
;-----------------------------------------------------------------------------
;aaZefirosSetMerge
;aaZefirosUndoEnchant
;-----------------------------------------------------------------------------

;Used globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;Given two enchantments, copies all effects of Ench1 over to Ench2, combining
;effects where necessary.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
ref Ench1
ref Ench2

short C1
short C2
short i
short j

long EffC
long AuxC

long Num
;-----------------------------------------------------------------------------



;-----------------------------------------------------------------------------
begin Function { Ench1, Ench2 }
;-----------------------------------------------------------------------------

	Let C1 := GetMagicItemEffectCount Ench1
	Let C2 := GetMagicItemEffectCount Ench2


	;For every effect in Ench1...
	Let i := 0
	while ( i < C1 )

		Let EffC := GetNthEffectItemCode Ench1 i
		Let AuxC := GetNthEffectItemActorValue Ench1 i


		;Find effect on Ench2
		Let j := Call aaZefirosGetEffectIndex EffC AuxC Ench2

		;If not present, add it
		if ( j == -1 )

			CopyNthEffectItem Ench1 Ench2 i


		;If present, mod it by Ench1's values
		else

			Let Num := GetNthEffectItemMagnitude Ench1 i
			ModNthEffectItemMagnitude Num Ench2 j

			Let Num := GetNthEffectItemArea Ench1 i
			ModNthEffectItemArea Num Ench2 j

			Let Num := GetNthEffectItemDuration Ench1 i
			ModNthEffectItemDuration Num Ench2 j


		endif

		Let i += 1
	loop


	Return
;-----------------------------------------------------------------------------
end ;Function { Ench1, Ench2 }
;-----------------------------------------------------------------------------