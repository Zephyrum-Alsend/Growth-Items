scn aaZefirosSetMergeEnch

;Main script
;-----------------------------------------------------------------------------
;aaZefirosManager
;-----------------------------------------------------------------------------

;Used scripts
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Related scripts
;-----------------------------------------------------------------------------
;aaZefirosSetMerge
;-----------------------------------------------------------------------------

;Used globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;Given two enchantments, copies all effects of Ench1 over to Ench2, combining
;effects where necessary.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
ref Ench1
ref Ench2

short C1
short C2
short i
short j

long EffC
long AuxC

long Num
;-----------------------------------------------------------------------------



;-----------------------------------------------------------------------------
begin Function { Ench1, Ench2 }
;-----------------------------------------------------------------------------

	Let C1 := GetMagicItemEffectCount Ench1
	Let C2 := GetMagicItemEffectCount Ench2


	;-----------------------------------------------------------------------------
	;For every effect in Ench1...
	;-----------------------------------------------------------------------------
	Let i := 0
	while ( i < C1 )

		Let EffC := GetNthEffectItemCode Ench1 i
		Let AuxC := GetNthEffectItemActorValue Ench1 i


		;-----------------------------------------------------------------------------
		;Effect has no Actor Value
		;-----------------------------------------------------------------------------
		if ( AuxC == 256 && MagicItemHasEffectCode EffC Ench2 )

			;Find the effect on Ench2
			Let j := 0
			while ( j < C2 )

				if ( EffC == GetNthEffectItemCode Ench2 j )
					;Mod Ench2's effect by Ench1's values

					Let Num := GetNthEffectItemMagnitude Ench1 i
					ModNthEffectItemMagnitude Num Ench2 j

					Let Num := GetNthEffectItemArea Ench1 i
					ModNthEffectItemArea Num Ench2 j

					Let Num := GetNthEffectItemDuration Ench1 i
					ModNthEffectItemDuration Num Ench2 j
					
					Break

				endif

				Let j += 1
			loop


		;-----------------------------------------------------------------------------
		;Effect has an Actor Value
		;-----------------------------------------------------------------------------
		elseif ( MagicItemHasEffectCode EffC Ench2 AuxC )

			;Find the effect on Ench2
			Let j := 0
			while ( j < C2 )

				;Effects with the same code can affect different actor values
				if ( EffC == GetNthEffectItemCode Ench2 j && AuxC == GetNthEffectItemActorValue Ench2 j )
					;Mod Ench2's effect by Ench1's values
					
					Let Num := GetNthEffectItemMagnitude Ench1 i
					ModNthEffectItemMagnitude Num Ench2 j

					Let Num := GetNthEffectItemArea Ench1 i
					ModNthEffectItemArea Num Ench2 j

					Let Num := GetNthEffectItemDuration Ench1 i
					ModNthEffectItemDuration Num Ench2 j
					
					Break

				endif

				Let j += 1
			loop


		;-----------------------------------------------------------------------------
		;Effect does not exist on Ench2
		;-----------------------------------------------------------------------------
		else

			CopyNthEffectItem Ench1 Ench2 i

		endif

		Let i += 1
	loop


	Return
;-----------------------------------------------------------------------------
end ;Function { Ench1, Ench2 }
;-----------------------------------------------------------------------------