scn aaZefirosSetMerge

;Main script
;-----------------------------------------------------------------------------
;aaZefirosManager
;-----------------------------------------------------------------------------

;Used scripts
;-----------------------------------------------------------------------------
;aaZefirosGetSlotShared
;aaZefirosSetMerge
;aaZefirosSetStat
;aaZefirosSetWeight
;aaZefirosRemoveAllEffectItems
;aaZefirosGetEnchantLevels
;aaZefirosSetEnchant
;aaZefirosSetResistEffects
;aaZefirosGetResistMatrix
;aaZefirosSetMergeEnch
;aaZefirosReorderEffects
;-----------------------------------------------------------------------------

;Related scripts
;-----------------------------------------------------------------------------
;aaZefirosSetMerge
;aaZefirosSetForm
;aaZefirosSetMimic
;-----------------------------------------------------------------------------

;Used globals
;-----------------------------------------------------------------------------
;aaZefrosLevel
;aaZefirosWeightBody
;aaZefirosWeightFoot
;aaZefirosWeightHand
;aaZefirosWeightHead
;aaZefirosWeightLegs
;aaZefirosWeightShld
;aaZefirosActvEnchBody
;aaZefirosActvEnchFoot
;aaZefirosActvEnchHand
;aaZefirosActvEnchHead
;aaZefirosActvEnchLegs
;aaZefirosActvEnchShld
;aaZefirosSlotBody
;aaZefirosSlotFoot
;aaZefirosSlotHand
;aaZefirosSlotHead
;aaZefirosSlotLegs
;aaZefirosSlotShld
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;Changes the slot(s) of Armr[i] to Slot and checks if any items of Armr need
;to be un/merged.  Returns False if aborted, True otherwise.
;If items are to be merged, aborts if not all are present in Player's 
;inventory.  Handles un/merging stats and enchantments.
;If no change in Armr[i]'s slot(s), simply returns.
;
;Does NOT handle mesh/ground/icon paths, use SetForm.
;
;Hvy:
;[0]Heavy AR multiplier
;[1]Heavy Weight multiplier
;
;Stat := Call aaZefirosGetArmorStats
;
;EnSt := Call aaZefirosGetEnchantData
;
;Armr:
;[0]Body
;[1]Foot
;[2]Hand
;[3]Head
;[4]Legs
;[5]Shld
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
long Slot
array_var Dflt
array_var Hvy
array_var Stat
array_var EnSt
short i
array_var Armr

short Cnt
short j
short Brk
short Eqp
long OldSlot
array_var Slots
array_var Stack
array_var Added
array_var Missing
array_var ReMerg
array_var Iter
ref Item
ref Object
ref Ench

short AR
float Wgt
float MHP
float CHP
long Val
ref InvR
;-----------------------------------------------------------------------------

;-----------------------------------------------------------------------------
begin Function { Slot, Dflt, Hvy, Stat, EnSt, i, Armr }
;-----------------------------------------------------------------------------

	Let Item := Armr[i]

	DebugPrint "%BChecking merges with %b%n%B...%b" Item

	
	;-----------------------------------------------------------------------------
	;Handle un/merging of items
	;-----------------------------------------------------------------------------
	Let OldSlot := GetBipedSlotMask Item

	if ( OldSlot == Slot )
	
		DebugPrint "Slot unchanged."
		SetFunctionValue 1
		Return

	else

		;-----------------------------------------------------------------------------
		;Add back merged items
		;-----------------------------------------------------------------------------
		DebugPrint "Slot changed..."

		Let Slots := ar_Map 0::Armr[3] 1::Armr[0] 2::Armr[4] 3::Armr[2] 4::Armr[1] 8::Armr[5]

		Let Stack := Call aaZefirosGetSlotShared Item Slots
		Let Added := ar_Construct Array ;Track added items incase of abort
		ForEach Iter <- Stack

			Let Object := *Iter

			;Checking ItemCount prevents dupes appearing when loading saves
			if ( Object != Item && 0 == Player.GetItemCount Object )

				DebugPrint "Returning %n." Object
				Player.AddItemNS Object 1
				ar_Append Added Object

			endif
			
		loop


		;-----------------------------------------------------------------------------
		;Remove merged items
		;-----------------------------------------------------------------------------
		DebugPrint "Marking for merge..."

		Let Eqp := Player.GetEquipped Item ;To make sure Item stays equipped at the end
		SetBipedSlotMask Slot Item

		Let Stack := Call aaZefirosGetSlotShared Item Slots
		Let Brk := 1
		Let Missing := ar_Construct Array ;Track removed items incase of abort
		Let ReMerg := ar_Construct Array ;Track recursive calls incase of abort
		ForEach Iter <- Stack

			Let Object := *Iter

			if ( Object != Item )

				;If the item is present, mark for merge
				if ( 0 < Player.GetItemCount Object )

					DebugPrint "Marked %n." Object

					;Have to account for Object also being merged
					Let Cnt := ar_Size Armr
					Let j := 0
					while ( j < Cnt )

						if eval( Object == Armr[j] )

							ar_Append ReMerg ar_Map 0::j 1::GetBipedSlotMask Object
							Let Brk := Call aaZefirosSetMerge Dflt[j] Dflt Hvy Stat EnSt j Armr

						endif

						Let j += 1
					loop
					
					ar_Append Missing Object

				else ;Item to be marked is not present

					;Flag abort
					Let Brk := 0
					Break

				endif

			endif
			
		loop


		;-----------------------------------------------------------------------------
		;Abort if an item needed to merge is not present
		;-----------------------------------------------------------------------------
		if ( Brk == 0 )

			DebugPrint "%n missing from inventory, aborting SetMerge." Object
			MessageEX "Unable to change form without %n." Object

			SetBipedSlotMask OldSlot Item

			;Remove returned items
			ForEach Iter <- Added

				Let Object := *Iter
				Player.RemoveItemNS Object 1

			loop

			;Remerge marked items
			DebugPrint "%BRemerging unmerged items.%b"
			ForEach Iter <- ReMerg

				Let j := Iter["value"][0]
				Let OldSlot := Iter["value"][1]

				Call  aaZefirosSetMerge OldSlot Dflt Hvy Stat EnSt j Armr

			loop

			SetFunctionValue 0
			Return


		;-----------------------------------------------------------------------------
		;Finish merging
		;-----------------------------------------------------------------------------
		else

			;-----------------------------------------------------------------------------
			DebugPrint "Reinitializing %n..." Item
			;-----------------------------------------------------------------------------
			SetDebugMode 0 ;Nerf console clog

			;Armor stats
			SetArmorType 0 Item
			Call aaZefirosSetStat Stat[i][0] Stat[i][1] Stat[i][2] Stat[i][3] Stat[i][4] Stat[i][5] Stat[i][6] Stat[i][7] aaZefirosLevel Item
			Let Stack := ar_Map 0::aaZefirosWeightBody 1::aaZefirosWeightFoot 2::aaZefirosWeightHand 3::aaZefirosWeightHead 4::aaZefirosWeightLegs 5::aaZefirosWeightShld
			Call aaZefirosSetWeight Hvy[0] Hvy[1] Stack[i] Item

			;Enchantment stats
			Let Ench := GetEnchantment Item
			if ( Ench )

				Call aaZefirosRemoveAllEffectItems Ench
				Let Stack := Call aaZefirosGetEnchantLevels i
				Let Iter := ar_Map 0::aaZefirosActvEnchBody 1::aaZefirosActvEnchFoot 2::aaZefirosActvEnchHand 3::aaZefirosActvEnchHead 4::aaZefirosActvEnchLegs 5::aaZefirosActvEnchShld
				Call aaZefirosSetEnchant EnSt Stack Iter[i] Ench
				if ( Ench != aaZefirosInit.EnchShld ) ;Shield shouldn't have resists, no other elegant way
					Call aaZefirosSetResistEffects ( &1 ) Dflt ( Call aaZefirosGetResistMatrix 0 ) ( &Ench )
				endif
				
			endif
			SetDebugMode aaZefirosDebug


			;-----------------------------------------------------------------------------
			;Equip returned items if Item is equipped
			;-----------------------------------------------------------------------------
			if ( Player.GetEquipped Item )

				ForEach Iter <- Added
	
					Let Object := *Iter
					Player.EquipItem2NS Object
	
				loop

			endif


			;-----------------------------------------------------------------------------
			DebugPrint "Merging..."
			;-----------------------------------------------------------------------------
			ForEach Iter <- Missing
			;-----------------------------------------------------------------------------
			
				Let Object := *Iter
				DebugPrint "	%n" Object
				

				;-----------------------------------------------------------------------------
				;Merge stats
				;-----------------------------------------------------------------------------
				Let AR := GetArmorAR Object
				Let AR += GetArmorAR Item
				
				Let Wgt := GetWeight Object
				Let Wgt += GetWeight Item
				
				Let Val := GetGoldValue Object
				Let Val += GetGoldValue Item
				
				Let MHP := GetObjectHealth Object
				Let MHP += GetObjectHealth Item

				Let Stack := Player.GetInvRefsForItem Object
				Let InvR := Stack[0]
				Let CHP := InvR.GetCurrentHealth

				Let Stack := Player.GetInvRefsForItem Item
				Let InvR := Stack[0]
				Let CHP += InvR.GetCurrentHealth

				SetArmorAR AR Item
				SetWeight Wgt Item
				SetObjectHealth MHP Item
				SetGoldValue_T Val Item
				InvR.SetCurrentHealth CHP


				;-----------------------------------------------------------------------------
				;Merge enchantments
				;-----------------------------------------------------------------------------
				;Will return false if either lacks an enchantment
				if ( GetEnchantment Object && Ench )

					Call aaZefirosSetMergeEnch ( GetEnchantment Object ) Ench

					Let Stack := ar_Construct Array
					Let Stack[0] := ar_Map 0::( GetMagicEffectCode RSFI ) 1::256
					Let Stack[1] := ar_Map 0::( GetMagicEffectCode RSFR ) 1::256
					Let Stack[2] := ar_Map 0::( GetMagicEffectCode RSSH ) 1::256
					Let Stack[3] := ar_Map 0::( GetMagicEffectCode RSMA ) 1::256
					Let Stack[4] := ar_Map 0::( GetMagicEffectCode RSNW ) 1::256
					Let Stack[5] := ar_Map 0::( GetMagicEffectCode RSPA ) 1::256
					Let Stack[6] := ar_Map 0::( GetMagicEffectCode RSDI ) 1::256
					Call aaZefirosReorderEffects Stack ( &Ench )

				endif


				;-----------------------------------------------------------------------------
				;Remove Object
				;-----------------------------------------------------------------------------
				Player.RemoveItemNS Object 1
				;OnUnEquip DebugPrint will activate here
			
			;-----------------------------------------------------------------------------
			loop ;Iter <- Missing
			;-----------------------------------------------------------------------------
			

			;-----------------------------------------------------------------------------
			;Update global
			;-----------------------------------------------------------------------------
			if ( i == 0 )

				DebugPrint "%BSlotBody updated."
				Let aaZefirosSlotBody := Slot

			elseif ( i == 1 )

				DebugPrint "%BSlotFoot updated."
				Let aaZefirosSlotFoot := Slot

			elseif ( i == 2 )

				DebugPrint "%BSlotHand updated."
				Let aaZefirosSlotHand := Slot

			elseif ( i == 3 )

				DebugPrint "%BSlotHead updated."
				Let aaZefirosSlotHead := Slot

			elseif ( i == 4 )

				DebugPrint "%BSlotLegs updated."
				Let aaZefirosSlotLegs := Slot

			elseif ( i == 5 )

				DebugPrint "%BSlotShld updated."
				Let aaZefirosSlotShld := Slot

			endif

			;Re-equip if necessary
			if ( Eqp )

				Player.EquipItem2NS Item

			endif

		;-----------------------------------------------------------------------------
		endif ;( Brk != 0 )
		;-----------------------------------------------------------------------------

	;-----------------------------------------------------------------------------
	endif ;( OldSlot == Slot )
	;-----------------------------------------------------------------------------

	
	SetFunctionValue 1
	Return
;-----------------------------------------------------------------------------
end ;Function { Slot, Dflt, Hvy, Stat, EnSt, i, Armr }
;-----------------------------------------------------------------------------