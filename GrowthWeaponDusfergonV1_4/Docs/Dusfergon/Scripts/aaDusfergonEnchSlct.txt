scn aaDusfergonEnchSlct

;Related scripts
;-----------------------------------------------------------------------------
;aaDusfergonManager
;aaDusfergonEnchInit
;aaDusfergonEnchLvl
;aaDusfergonIncrVal
;-----------------------------------------------------------------------------

;Related globals
;-----------------------------------------------------------------------------
;aaDusfergonEnch
;aaDusfergonEnch1
;aaDusfergonEnch2
;aaDusfergonEnch3
;aaDusfergonEnch4
;aaDusfergonEnch5
;aaDusfergonEnch6
;aaDusfergonEnch7
;aaDusfergonEnch8
;aaDusfergonEnch9
;aaDusfergonEnch10
;-----------------------------------------------------------------------------

;Description
;-----------------------------------------------------------------------------
;According to globals above, checks if the appropriate effect set has been 
;added to the passed enchantment.  If not, adds the effects with base values 
;defined in Dusfergon.ini.
;Returns total Cost for book keeping.
;-----------------------------------------------------------------------------

;Parameters
;-----------------------------------------------------------------------------
long MagnitudeDG
long AreaDG
long DurationDG
long MagnitudeWK
long AreaWK
long DurationWK
float Cost ;Return value

short EnchIter
short DGFALv
short WKMALv

ref Ench
;-----------------------------------------------------------------------------



;-----------------------------------------------------------------------------
begin Function { Ench, Cost }
;-----------------------------------------------------------------------------
	
	;-----------------------------------------------------------------------------
	;Ensure Damage Fatigue and Weakness to Magic are always last
	;-----------------------------------------------------------------------------
	if ( MagicItemHasEffect DGFA Ench )

		if ( MagicItemHasEffect WKMA Ench )

			Set EnchIter to ( GetMagicItemEffectCount Ench ) - 2

		else

			Set EnchIter to ( GetMagicItemEffectCount Ench ) - 1

		endif

		Set MagnitudeDG to GetNthEffectItemMagnitude Ench EnchIter
		Set AreaDG to GetNthEffectItemArea Ench EnchIter
		Set DurationDG to GetNthEffectItemDuration Ench EnchIter

		RemoveNthEffectItem Ench EnchIter

		Set DGFALv to 1

	else

		Set DGFALv to 0

	endif
	
	if ( MagicItemHasEffect WKMA Ench )

		Set EnchIter to ( GetMagicitemEffectCount Ench ) - 1
					
		Set MagnitudeWK to GetNthEffectItemMagnitude Ench EnchIter
		Set AreaWK to GetNthEffectItemArea Ench EnchIter
		Set DurationWK to GetNthEffectItemDuration Ench EnchIter

		RemoveNthEffectItem Ench EnchIter

		Set WKMALv to 1

	else

		Set WKMALv to 0

	endif

	;-----------------------------------------------------------------------------
	;Fire option
	;-----------------------------------------------------------------------------
	if ( aaDusfergonEnch == 1 && aaDusfergonEnch1 == 0 )
	;-----------------------------------------------------------------------------
		if ( WKMALv == 0 )

			Set MagnitudeWK to aaDusfergonInit.sWKMAMagnBase
			Set AreaWK to aaDusfergonInit.sWKMAAreaBase
			Set DurationWK to aaDusfergonInit.sWKMADuraBase				
			Set Cost to Cost + aaDusfergonInit.fWKMACostBase

			Set WKMALv to 1

		endif

		AddFullEffectItem FIDG aaDusfergonInit.sELDGMagnBase aaDusfergonInit.sELDGAreaBase aaDusfergonInit.sELDGDuraBase 1 Ench
		AddFullEffectItem WKFI aaDusfergonInit.sWKELMagnBase aaDusfergonInit.sWKELAreaBase aaDusfergonInit.sWKELDuraBase 1 Ench
		Set Cost to Cost + ( aaDusfergonInit.fELDGCostBase + aaDusfergonInit.fWKELCostBase )

	;-----------------------------------------------------------------------------
	;Ice option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 2 && aaDusfergonEnch2 == 0 )
	;-----------------------------------------------------------------------------
		if ( WKMALv == 0 )

			Set MagnitudeWK to aaDusfergonInit.sWKMAMagnBase
			Set AreaWK to aaDusfergonInit.sWKMAAreaBase
			Set DurationWK to aaDusfergonInit.sWKMADuraBase				
			Set Cost to Cost + aaDusfergonInit.fWKMACostBase

			Set WKMALv to 1

		endif

		AddFullEffectItem FRDG aaDusfergonInit.sELDGMagnBase aaDusfergonInit.sELDGAreaBase aaDusfergonInit.sELDGDuraBase 1 Ench
		AddFullEffectItem WKFR aaDusfergonInit.sWKELMagnBase aaDusfergonInit.sWKELAreaBase aaDusfergonInit.sWKELDuraBase 1 Ench
		Set Cost to Cost + ( aaDusfergonInit.fELDGCostBase + aaDusfergonInit.fWKELCostBase )
			
	;-----------------------------------------------------------------------------
	;Volt option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 3 && aaDusfergonEnch3 == 0 )
	;-----------------------------------------------------------------------------
		if ( WKMALv == 0 )

			Set MagnitudeWK to aaDusfergonInit.sWKMAMagnBase
			Set AreaWK to aaDusfergonInit.sWKMAAreaBase
			Set DurationWK to aaDusfergonInit.sWKMADuraBase				
			Set Cost to Cost + aaDusfergonInit.fWKMACostBase

			Set WKMALv to 1

		endif

		AddFullEffectItem SHDG aaDusfergonInit.sELDGMagnBase aaDusfergonInit.sELDGAreaBase aaDusfergonInit.sELDGDuraBase 1 Ench
		AddFullEffectItem WKSH aaDusfergonInit.sWKELMagnBase aaDusfergonInit.sWKELAreaBase aaDusfergonInit.sWKELDuraBase 1 Ench
		Set Cost to Cost + ( aaDusfergonInit.fELDGCostBase + aaDusfergonInit.fWKELCostBase )

	;-----------------------------------------------------------------------------
	;Knight-Slayer option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 4 && aaDusfergonEnch4 == 0 )
	;-----------------------------------------------------------------------------
		if ( DGFALv == 0 )

			Set MagnitudeDG to aaDusfergonInit.sDGFAMagnBase
			Set AreaDG to aaDusfergonInit.sDGFAAreaBase
			Set DurationDG to aaDusfergonInit.sDGFADuraBase				
			Set Cost to Cost + aaDusfergonInit.fDGFACostBase

			Set DGFALv to 1

		endif

		AddFullEffectItem DIWE aaDusfergonInit.sDIWEMagnBase aaDusfergonInit.sDIWEAreaBase aaDusfergonInit.sDIWEDuraBase 1 Ench
		AddFullEffectItem DIAR aaDusfergonInit.sDIARMagnBase aaDusfergonInit.sDIARAreaBase aaDusfergonInit.sDIARDuraBase 1 Ench
		Set Cost to Cost + ( aaDusfergonInit.fDIWECostBase + aaDusfergonInit.fDIARCostBase )

	;-----------------------------------------------------------------------------
	;Mage-Slayer option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 5 && aaDusfergonEnch5 == 0 )
	;-----------------------------------------------------------------------------
		AddFullEffectItem DGSP aaDusfergonInit.sDGSPMagnBase aaDusfergonInit.sDGFAAreaBase aaDusfergonInit.sDGFADuraBase 1 Ench
		AddFullEffectItem DSPL aaDusfergonInit.sDSPLMagnBase aaDusfergonInit.sDSPLAreaBase 0 1 Ench
		AddFullEffectItem SLNC 0 aaDusfergonInit.sSLNCAreaBase aaDusfergonInit.sSLNCDuraBase 1 Ench
		Set Cost to Cost + ( aaDusfergonInit.fDGSPCostBase + aaDusfergonInit.fDSPLCostBase + aaDusfergonInit.fSLNCCostBase )
			
	;-----------------------------------------------------------------------------
	;Weaken option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 6 && aaDusfergonEnch6 == 0 )
	;-----------------------------------------------------------------------------		
		if ( WKMALv == 0 )

			Set MagnitudeWK to aaDusfergonInit.sWKMAMagnBase
			Set AreaWK to aaDusfergonInit.sWKMAAreaBase
			Set DurationWK to aaDusfergonInit.sWKMADuraBase			
			Set Cost to Cost + aaDusfergonInit.fWKMACostBase

			Set WKMALv to 1

		endif

		AddFullEffectItem BRDN aaDusfergonInit.sBRDNMagnBase aaDusfergonInit.sBRDNAreaBase aaDusfergonInit.sBRDNDuraBase 1 Ench
		Set EnchIter to AddFullEffectItem DRAT aaDusfergonInit.sDRATMagnBase aaDusfergonInit.sDRATAreaBase aaDusfergonInit.sDRATDuraBase 1 Ench
		SetNthEffectItemActorValue "Strength" Ench EnchIter
		Set Cost to Cost + ( aaDusfergonInit.fBRDNCostBase + aaDusfergonInit.fDRATCostBase )

	;-----------------------------------------------------------------------------
	;Subjugate option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 7 && aaDusfergonEnch7 == 0 )
	;-----------------------------------------------------------------------------
		AddFullEffectItem COCR aaDusfergonInit.sCOMMMagnBase aaDusfergonInit.sCOMMAreaBase aaDusfergonInit.sCOMMDuraBase 1 Ench
		AddFullEffectItem COHU aaDusfergonInit.sCOMMMagnBase aaDusfergonInit.sCOMMAreaBase aaDusfergonInit.sCOMMDuraBase 1 Ench
		AddFullEffectItem RALY aaDusfergonInit.sRALYMagnBase aaDusfergonInit.sRALYAreaBase aaDusfergonInit.sRALYDuraBase 1 Ench
		Set Cost to Cost + ( aaDusfergonInit.fCOMMCostBase + aaDusfergonInit.fCOMMCostBase + aaDusfergonInit.fRALYCostBase )

	;-----------------------------------------------------------------------------
	;Corruption option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 8 && aaDusfergonEnch8 == 0 )
	;-----------------------------------------------------------------------------
		AddFullEffectItem FRNZ aaDusfergonInit.sFRNZMagnBase aaDusfergonInit.sFRNZAreaBase aaDusfergonInit.sFRNZDuraBase 1 Ench
		AddFullEffectItem LGHT aaDusfergonInit.sLGHTMagnBase aaDusfergonInit.sLGHTAreaBase aaDusfergonInit.sLGHTDuraBase 1 Ench
		AddFullEffectItem STRP 0 aaDusfergonInit.sSTRPAreaBase aaDusfergonInit.sSTRPDuraBase 1 Ench
		Set Cost to Cost + ( aaDusfergonInit.fFRNZCostBase + aaDusfergonInit.fLGHTCostBase + aaDusfergonInit.fSTRPCostBase )

	;-----------------------------------------------------------------------------
	;Unbalance option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 9 && aaDusfergonEnch9 == 0 )
	;-----------------------------------------------------------------------------		
		if ( DGFALv == 0 )

			Set MagnitudeDG to aaDusfergonInit.sDGFAMagnBase
			Set AreaDG to aaDusfergonInit.sDGFAAreaBase
			Set DurationDG to aaDusfergonInit.sDGFADuraBase				
			Set Cost to Cost + aaDusfergonInit.fDGFACostBase

			Set DGFALv to 1

		endif

		if ( WKMALv == 0 )

			Set MagnitudeWK to aaDusfergonInit.sWKMAMagnBase
			Set AreaWK to aaDusfergonInit.sWKMAAreaBase
			Set DurationWK to aaDusfergonInit.sWKMADuraBase			
			Set Cost to Cost + aaDusfergonInit.fWKMACostBase

			Set WKMALv to 1

		endif

		Set EnchIter to AddFullEffectItem DRAT aaDusfergonInit.sDRATMagnBase aaDusfergonInit.sDRATAreaBase aaDusfergonInit.sDRATDuraBase 1 Ench
		SetNthEffectItemActorValue "Agility" Ench EnchIter
		Set Cost to Cost + ( aaDusfergonInit.fDRATCostBase )

	;-----------------------------------------------------------------------------
	;Subjugate option
	;-----------------------------------------------------------------------------
	elseif ( aaDusfergonEnch == 10 && aaDusfergonEnch10 == 0 )
	;-----------------------------------------------------------------------------
		AddFullEffectItem ABFA aaDusfergonInit.sABFAMagnBase aaDusfergonInit.sABFAAreaBase aaDusfergonInit.sABFADuraBase 1 Ench
		AddFullEffectItem ABHE aaDusfergonInit.sABHEMagnBase aaDusfergonInit.sABHEAreaBase aaDusfergonInit.sABHEDuraBase 1 Ench
		AddFullEffectItem ABSP aaDusfergonInit.sABSPMagnBase aaDusfergonInit.sABSPAreaBase aaDusfergonInit.sABSPDuraBase 1 Ench
		Set Cost to Cost + ( aaDusfergonInit.fABFACostBase + aaDusfergonInit.fABHECostBase + aaDusfergonInit.fABSPCostBase )

	;-----------------------------------------------------------------------------
	endif ;( aaDusfergonEnch != 0 )
	;-----------------------------------------------------------------------------

	;-----------------------------------------------------------------------------
	;Append Damage Fatigue and Weakness to Magic
	;-----------------------------------------------------------------------------
	if ( DGFALv > 0 )

		AddFullEffectItem DGFA MagnitudeDG AreaDG DurationDG 1 Ench

	endif
	
	if ( WKMALv > 0 )

		AddFullEffectItem WKMA MagnitudeWK AreaWK DurationWK 1 Ench

	endif


	SetEnchantmentCost Cost Ench

	SetFunctionValue Cost
	Return
;-----------------------------------------------------------------------------
end ;Function { Ench, Cost }
;-----------------------------------------------------------------------------